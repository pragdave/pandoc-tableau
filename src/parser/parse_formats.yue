--                              Global     Selector
--     fg(color)                   Y          Y
--     bg(color)                   Y          Y
--     align([lcrj][tmb])          Y          Y
--     width(n)                    Y          Y 
--     lines([ []_~ ]+)            N          Y 
--     style(a,b,c)                Y          Y
--     header                      N          Y
--     footer                      N          Y
--     small                       Y          Y
--
--     cols( colspec, colspec...)  Y          N
--     hlines                      Y          N
--     vlines                      Y          N
--     boxed                       Y          N

import "parser.util" as { :dump, :expect }

const color_names = {
  shade: 1,
  shade1: 1,
  shade2: 1,
  shade3: 1,
  shade4: 1,
  shade5: 1,
  shade6: 1,
  shade7: 1,
  shade8: 1,
  shade9: 1,
}

const color_hx3 = "#%x%x%x"
const color_hx6 = "#%x%x%x%x%x%x"

gather_one_colspec = () -> "to be defined below"

--------------------------------------------------------------------------------
named_color = (text) ->
  text\save_place()
  name = text\scan("%w+%f[%W]")
  unless name and color_names[name]
    text\restore_place()
    return nil

  suffix = text\scan("%d%f[%D]") |> tonumber
  { color_type: "named-color", :name, :suffix }

--------------------------------------------------------------------------------
known_color = (text) ->
  rgb = text\scan(color_hx6) or text\scan(color_hx3) 
  if rgb
    { color_type: "rgb", name: rgb }
  else
    named_color(text)

--------------------------------------------------------------------------------
-- any word
any_color = (text) ->
  col = text\scan("[%w%-%_]+%f[^%w%-%_]")
  if col
    { color_type: "any-color", name: col }

--------------------------------------------------------------------------------
--     align([lcrj][tmb])   l c r j t m b
align = (text) ->
  spec = nil
  if text\scan("align%(")
    spec = expect(align, text, "expecting alignment specifier [lcrj][tmb]")
    text\expect("%)", "expected closing parenthesis after alignment specifier")
  else
    if text\scan("[lcrj][tmb]%f[%A]") or text\scan("[lcrj]%f[%A]") or text\scan("[tmb]%f[%A]")
      spec = { type: "align", spec: text.last_match }
  spec

--------------------------------------------------------------------------------
--     boxed
boxed = (text) ->
  if text\scan("boxed%f[%A]") or text\scan("box%f[%A]")
    { type: "boxed" }

--------------------------------------------------------------------------------
--     cols(colspec, ...)
cols = (text) ->
  return unless text\scan("cols?%(%s*")
  specs = {}
  spec = gather_one_colspec(text)
  while spec
    specs[]= spec
    text\skip("%s*,%s*")
    spec = gather_one_colspec(text)
  text\expect("%s*%)%s*", "missing close parenthesis after colspecs")
  specs

--------------------------------------------------------------------------------
--    any color
color = (text, ground) ->
  spec = nil
  if text\scan("#{ground}%(")
    unless spec = known_color(text)
      spec = expect(any_color, text, "expecting color")
    text\expect("%)", "expected a single-word color name followed by a ')' ")

  if spec
    text\skip("%s*")
    spec.type = ground
  spec

--------------------------------------------------------------------------------
--     bg/fg(color)
bg = (text) ->
  spec = color(text, "bg") or known_color(text)
  if spec
    spec.type = "bg"
  spec 

fg = (text) ->
  color(text, "fg")


--------------------------------------------------------------------------------
--     footer
footer = (text) ->
  if text\scan("footer%f[%A]") or text\scan("foot%f[%A]")
    { type: "footer" }

--------------------------------------------------------------------------------
--     header
header = (text) ->
  if text\scan("header%f[%A]") or text\scan("head%f[%A]")
    { type: "header" }

--------------------------------------------------------------------------------
--     hlines
hlines = (text) ->
  if text\scan("hlines%f[%A]")
    { type: "hlines" }

--------------------------------------------------------------------------------
--     large
large = (text) ->
  match,xs = text\scan("(x*)large%f[%A]")
  if match
    { type: "large", amount: if xs then #xs else 0 }

--------------------------------------------------------------------------------
--     lines([tblrx]+) 
lines = (text) ->
  spec = nil
  if text\scan("lines?%(")
    spec = expect(lines, text, "expecting line specifier, one or more of: t, b, l, r, x")
    text\expect("%)", "expected closing parenthesis after line specifier")
  else
    if text\scan("[tblrx]+")
      spec = { type: "lines", spec: text.last_match }
  spec

--------------------------------------------------------------------------------
--     normal  (font size)
normal = (text) ->
  if text\scan("normal%f[%A]")
    { type: "normal" }

--------------------------------------------------------------------------------
--     small
small = (text) ->
  match,xs = text\scan("(x*)small%f[%A]")
  if match
    { type: "small", amount: if xs then #xs else 0 }

--------------------------------------------------------------------------------
-- span
span = (text) ->
  if text\scan("span%f[%A]%s*")
    { type: "span" }

--------------------------------------------------------------------------------
--     style = .name
style = (text) ->
  spec = nil
  if text\scan("%.[%w_-]+")
    spec = string.sub(text.last_match, 2)
    text\scan("%s*")
  
  if spec
    { type: "style", :spec }


--------------------------------------------------------------------------------
--     vlines
vlines = (text) ->
  if text\scan("vlines%f[%A]")
    { type: "vlines" }

--------------------------------------------------------------------------------
--     width(n)             [0-9]%?
width = (text) ->
  spec = nil
  if text\scan("width%(")
    spec = expect(width, text, "expecting an integer width")
    text\expect("%)", "expected closing parenthesis after width")
  else
    if text\scan("0?%.[1-9][0-9]*") or text\scan("[1-9][0-9]*") 

      wid = text.last_match |> tonumber 
      if text\scan("%%")
        wid = wid / 100.0
      spec = { type: "width", spec: wid }
  spec



--------------------------------------------------------------------------------
gather_one_colspec = (text) ->
  specs = {}
  valid_specs = (text) -> align(text) or width(text) or lines(text) or fg(text) or bg(text)
  spec = valid_specs(text)
  while spec
    specs[] = spec
    text\skip("%s*")
    spec = valid_specs(text)
  specs


--------------------------------------------------------------------------------
export parse_global_formats = (text) ->
  align(text)     or
    bg(text)      or
    boxed(text)   or 
    cols(text)    or
    fg(text)      or
    hlines(text)  or
    large(text)   or
    normal(text)  or
    small(text)   or
    style(text)   or
    vlines(text)  or
    width(text)

--------------------------------------------------------------------------------
export parse_selector_formats = (text) ->
  text\skip("%s+")
  return if text\empty()

  align(text)    or
    bg(text)     or
    fg(text)     or
    footer(text) or
    header(text) or
    large(text)  or
    normal(text) or
    small(text)  or
    span(text)   or
    style(text)  or
    width(text)  or
    lines(text)      -- lines must go at the end, because it swallows single characters

